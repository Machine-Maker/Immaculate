import org.gradle.api.attributes.plugin.GradlePluginApiVersion

plugins {
    id 'java-gradle-plugin'
}

['apiElements', 'runtimeElements'].each {
    configurations.named(it).configure {
        attributes {
            attribute(
                    GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE,
                    objects.named(GradlePluginApiVersion, gradle.gradleVersion)
            )
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r'
}

gradlePlugin {
    plugins {
        immaculatePlugin {
            id = 'dev.lukebemish.immaculate'
            implementationClass = 'dev.lukebemish.immaculate.ImmaculatePlugin'
        }
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Version': version as String
        )
    }
}

tasks.named('wrapper', Wrapper) {
    distributionType = Wrapper.DistributionType.ALL
}
