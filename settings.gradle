pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://maven.lukebemish.dev/releases/' }
    }
}

plugins {
    id 'dev.lukebemish.conventions' version '0.1.10'
    id 'dev.lukebemish.managedversioning' version '1.2.19' apply false
}

gradle.lifecycle.beforeProject {
    group = 'dev.lukebemish' + (project.path == ':' ? '' : '.immaculate' + project.path.split(':')[0..-2].join('.'))

    apply plugin: 'dev.lukebemish.managedversioning'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    managedVersioning {
        versionFile.set rootProject.isolated.projectDirectory.file('version.properties')
        versionPRs()
        versionSnapshots()
    }

    managedVersioning.apply()
    managedVersioning.publishing.mavenSnapshot(publishing)
    managedVersioning.publishing.mavenStaging(publishing)
    managedVersioning.publishing.mavenPullRequest(publishing)

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    tasks.register('publishCentral') {
        group = 'publishing'
    }
}

rootProject.name = 'immaculate'

include 'wrapper'
include 'wrapper:eclipse-jdt'
include 'wrapper:google-java-format'
