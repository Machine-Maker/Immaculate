import org.gradle.api.attributes.plugin.GradlePluginApiVersion

plugins {
    id 'groovy-gradle-plugin'
}

group = 'dev.lukebemish'
version = '0.1.0'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

['apiElements', 'runtimeElements'].each {
    configurations.named(it).configure {
        attributes {
            attribute(
                    GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE,
                    objects.named(GradlePluginApiVersion, gradle.gradleVersion)
            )
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-text:1.12.0'
    api 'net.neoforged:groovydslimprover:1.0.15'
}

gradlePlugin {
    plugins {
        immaculatePlugin {
            id = 'dev.lukebemish.immaculate'
            implementationClass = 'dev.lukebemish.immaculate.ImmaculatePlugin'
        }
    }
}

tasks.named('wrapper', Wrapper) {
    distributionType = Wrapper.DistributionType.ALL
}
